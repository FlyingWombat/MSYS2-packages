pkgname=kakoune
pkgver=2020.01.16
pkgrel=1
pkgdesc='Multiple-selection, UNIX-flavored modal editor'
arch=('x86_64' 'armv7h')
url='https://github.com/mawww/kakoune'
license=('custom:unlicense')
depends=(
  'ncurses-devel'
)
optdepends=(
  'aspell: spell checking support'
  'clang: add command for C/CPP insert mode completion support'
  'ranger: provides file explorer functionality'
  'tmux: tabbing support'
  'universal-ctags-git: provides `readtags` used by `:tag` command to jump on a tag definition'
  'xdotool: add support for kak windows switching (in grep, make... files)'
  'xorg-xmessage: for nicer debugging messages'
)
makedepends=(
  'gcc'
)
provides=('kakoune')
conflicts=('kakoune')
source=("https://github.com/mawww/kakoune/releases/download/v${pkgver}/kakoune-${pkgver}.tar.bz2"
       'msys-makefile.patch')
sha256sums=('a094f1689f0228308f631e371b382b0c0522391fc8b6c23a6cbc71ff404a0dae'
            '6f0ee1a193bf5e45b2d94627e3fd93f409e4a11e6e19b3a6ace1c8bb8184c09d')

# https://github.com/msys2/MSYS2-packages/issues/1216
export MSYS="winsymlinks:lnk"

prepare(){
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -p1 < ../msys-makefile.patch

  # Msys2 removed unix symlinks
  if [[ `uname` == *"MSYS"* ]]; then
    rm -f share/kak/autoload
    cp -r rc share/kak/autoload
    rm -f share/kak/colors
    cp -r colors share/kak/colors
    rm -f share/kak/doc
    cp -r doc/pages share/kak/doc
    rm -f share/kak/gdb
    cp -r gdb share/kak/gdb
    rm -f share/kak/rc
    cp -r rc share/kak/rc
  fi
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}/src"
  make test
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}/src"
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/src"
  make install-strip DESTDIR="${pkgdir}" PREFIX=/usr
  install -D ../UNLICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE

  # https://github.com/mawww/kakoune/wiki/Windows-tips
  mv "${pkgdir}/usr/bin/kak" "${pkgdir}/usr/bin/kak.exe"
}
